""" rules for scraps counting """

rule assign_sites_R1:
  input:
    "{results}/{sample}/{sample}_R1_Aligned.sortedByCoord.out.bam"
  output:
    bam = temp("{results}/temp/{sample}_R1_assigned.bam")
  params:
    job_name = "assign_sites_R1",
    out = "{results}/temp/{sample}_R1_assigned",
    out_bam = "{results}/temp/{sample}_R1.bam.featureCounts.bam"
  log:
    "{results}/logs/assign_sites_{sample}_R1.txt"
  threads:
    12
  resources:
    mem_mb = 8000
  shell:
    r"""
    if echo {POLYA_SITES} | tr '[:upper:]' '[:lower:]' | grep -q -e "\.saf$" -e "\.saf.gz$"; then
      polyaformat='SAF'; else
      polyaformat='GTF';
    fi

    featureCounts \
    -s 2 -Q 10 -O \
    --read2pos 5 \
    -o {params.out} \
    -F $polyaformat \
    -a {POLYA_SITES} \
    -R BAM \
    -T {threads} \
    {input} \
    2> {log}

    samtools sort \
    {params.out_bam} \
    -o {output.bam}

    samtools index {output.bam}
    rm -rf {params.out_bam}
    """

rule assign_sites_R2:
  input:
    "{results}/temp/{sample}_R2.bam"
  output:
    bam = temp("{results}/temp/{sample}_R2_assigned.bam")
  params:
    job_name = "assign_sites_R2",
    out = "{results}/temp/{sample}_R2_assigned",
    out_bam = "{results}/temp/{sample}_R2.bam.featureCounts.bam"
  log:
    "{results}/logs/assign_sites_{sample}_R2.txt"
  threads:
    12
  resources:
    mem_mb = 8000
  shell:
    r"""
    if echo {POLYA_SITES} | tr '[:upper:]' '[:lower:]' | grep -q -e "\.saf$" -e "\.saf.gz$"; then
      polyaformat='SAF'; else
      polyaformat='GTF';
    fi

    featureCounts \
    -s 1 -Q 10 -O \
    --read2pos 3 \
    -o {params.out} \
    -F $polyaformat \
    -a {POLYA_SITES} \
    -R BAM \
    -T {threads} \
    {input} \
    2> {log}
    
    samtools sort \
    {params.out_bam} \
    -o {output.bam}
    
    samtools index {output.bam}

    rm -rf {params.out_bam}
    """

rule assign_sites_paired:
  input:
    "{results}/{sample}/{sample}_paired_Aligned.sortedByCoord.out.bam"
  output:
    bam = temp("{results}/temp/{sample}_paired_assigned.bam")
  params:
    job_name = "assign_sites_paired",
    out = "{results}/temp/{sample}_paired_assigned",
    out_bam = "{results}/temp/{sample}_paired.bam.featureCounts.bam"
  log:
    "{results}/logs/assign_sites_{sample}_paired.txt"
  threads:
    12
  resources:
    mem_mb = 8000
  shell:
    r"""
    if echo {POLYA_SITES} | tr '[:upper:]' '[:lower:]' | grep -q -e "\.saf$" -e "\.saf.gz$"; then
      polyaformat='SAF'; else
      polyaformat='GTF';
    fi

    featureCounts \
    -s 2 -Q 10 -O -p \
    --read2pos 5 \
    -o {params.out} \
    -F $polyaformat \
    -a {POLYA_SITES} \
    -R BAM \
    -T {threads} \
    {input} \
    2> {log}

    samtools sort \
    {params.out_bam} \
    -o {output.bam}

    samtools index {output.bam}
    rm -rf {params.out_bam}
    """
    
rule filter_R2:
  input:
    "{results}/{sample}/{sample}_R2_Aligned.sortedByCoord.out.bam"
  output:
    temp("{results}/temp/{sample}_R2.bam")
  params:
    job_name = "filter",
    temp = "{results}/temp/{sample}_R2_filter.bam",
    tempbai = "{results}/temp/{sample}_R2_filter.bam.bai"
  log:
    "{results}/logs/filter_{sample}_R2.txt"
  threads:
    4
  resources:
    mem_mb = 8000
  shell:
    r"""
    samtools view -h {input} | grep -v 'CB:Z:-\|UB:Z:-' | samtools view -b - > {params.temp}
    samtools index {params.temp}
    python3 inst/scripts/filter_bam.py -i {params.temp} -o {output}
    samtools index {output}
    rm -rf {params.temp}
    rm -rf {params.tempbai}
    """

rule count:
  input:
    "{results}/temp/{sample}_{read}_assigned.bam"
  output:
    "{results}/counts/{sample}_{read}_counts.tsv.gz"
  params:
    job_name = "count"
  log:
    "{results}/logs/count_{sample}_{read}.txt"
  threads:
    4
  resources:
    mem_mb = 8000
  shell:
    r"""
    umi_tools count \
      --per-gene \
      --gene-tag=XT \
      --assigned-status-tag=XS \
      --per-cell \
      --extract-umi-method=tag \
      --umi-tag=UB \
      --cell-tag=CB \
      -I {input} \
      -S {output} \
      > {log}
    """

rule bed_R1:
  input:
    "{results}/{sample}/{sample}_R1_Aligned.sortedByCoord.out.bam"
  output:
    "{results}/bed/{sample}_R1.bed.gz"
  params:
    job_name = "bed",
    dedup = "{results}/temp/{sample}_R1_dedup.bam",
    r = "{results}/temp/{sample}_R1_r.bam"
  log:
    "{results}/logs/bed_{sample}_R1.txt"
  threads:
    4
  resources:
    mem_mb = 48000
  shell:
    r"""
    umi_tools dedup \
      --extract-umi-method=tag \
      --umi-tag=UB \
      --per-cell \
      --cell-tag=CB \
      --paired \
      --method=unique \
      -I {input} \
      -S {params.dedup} \
      > {log}
    samtools view -F 4 {params.dedup} -b > {params.r}
    bedtools genomecov -ibam {params.r} -5 -dz | awk 'BEGIN {{ OFS = "\t" }} {{ print $1, $2 - 1, $2, $3 }}' - | g
zip > {output}
    rm -rf {params.r}
    """

rule bed_R2:
  input:
    "{results}/temp/{sample}_R2.bam"
  output:
    "{results}/bed/{sample}_R2.bed.gz"
  params:
    job_name = "bed",
    dedup = "{results}/temp/{sample}_R2_dedup.bam"
  log:
    "{results}/logs/bed_{sample}_R2.txt"
  threads:
    4
  resources:
    mem_mb = 48000
  shell:
    r"""
    umi_tools dedup \
      --extract-umi-method=tag \
      --umi-tag=UB \
      --per-cell \
      --cell-tag=CB \
      --method=unique \
      -I {input} \
      -S {params.dedup} \
      > {log}

    bedtools genomecov -ibam {params.dedup} -3 -dz | awk 'BEGIN {{ OFS = "\t" }} {{ print $1, $2 - 1, $2, $3 }}' - | gzip > {output}
    """

rule bed_paired:
  input:
    "{results}/{sample}/{sample}_paired_Aligned.sortedByCoord.out.bam"
  output:
    "{results}/bed/{sample}_paired.bed.gz"
  params:
    job_name = "bed",
    dedup = "{results}/temp/{sample}_paired_dedup.bam",
    r = "{results}/temp/{sample}_paired_r.bam"
  log:
    "{results}/logs/bed_{sample}_paired.txt"
  threads:
    4
  resources:
    mem_mb = 48000
  shell:
    r"""
    umi_tools dedup \
      --extract-umi-method=tag \
      --umi-tag=UB \
      --per-cell \
      --cell-tag=CB \
      --paired \
      --method=unique \
      -I {input} \
      -S {params.dedup} \
      > {log}
    # 0x82 for R2
    samtools view -f 0x42 {params.dedup} -b > {params.r}
    bedtools genomecov -ibam {params.r} -5 -dz | awk 'BEGIN {{ OFS = "\t" }} {{ print $1, $2 - 1, $2, $3 }}' - | gzip > {output}
    rm -rf {params.r}
    """
